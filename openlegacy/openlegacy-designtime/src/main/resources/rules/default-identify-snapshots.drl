package openlegacy;
rule "find-identifiers"
ruleflow-group "identify-snapshots"
when
	$titleRegex    : String()  from "[A-Za-z]+[ ][^ ^.]*"
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()
	// which has a snaphot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.snapshot
	 
	// collect all the other snapshots
	$otherSnapshots: List() from accumulate ( s: ScreenEntityDesigntimeDefinition(this != $screenEntityDefinition), collectList(s.getSnapshot()) )

	$field: TerminalField(value matches $titleRegex, editable == false) from $snapshot.getFields() 

	// and this fields doesn't exist on one of the other snapshots (equals by length, text, position and editable) 
	not TerminalSnapshot(fields contains $field) from $otherSnapshots 
then
	screenEntityDefinitionsBuilder.addIdentifier(snapshotsAnalyzerContext,$screenEntityDefinition,$field);
end