package openlegacy;
rule "identify-snapshots"
ruleflow-group "identify-snapshots"
when
	$ruleParameterSet: RuleParametersSet(ruleId == "identify-snapshots")
	$identifierRegex    : String()  from (String)$ruleParameterSet.getRuleParameters().get("identifierRegex");
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()
	// which has a snaphot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.snapshot
	 
	// collect all the other snapshots
	$otherSnapshots: List() from accumulate ( s: ScreenEntityDesigntimeDefinition(this != $screenEntityDefinition), collectList(s.getSnapshot()) )

	$field: TerminalField(value matches $identifierRegex, editable == false) from $snapshot.getFields() 

	// and this fields doesn't exist on one of the other snapshots (equals by length, text, position and editable) 
	not TerminalSnapshot(fields contains $field) from $otherSnapshots 
then
	screenEntityDefinitionsBuilder.addIdentifier(snapshotsAnalyzerContext,$screenEntityDefinition,$field);
end