package openlegacy;

rule "find-screen-name"
ruleflow-group "find-screen-name"
when
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()
	
	$ruleParameterSet: RuleParametersSet(ruleId == "find-screen-name")
	$titleRegex    : String()  from (String)$ruleParameterSet.getRuleParameters().get("titleRegex");
	$titleStartRowParam  : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("titleStartRow");
	$titleEndRowParam    : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("titleEndRow");

	// considering possible window borders
	$borderStartRow: Integer() from $screenEntityDefinition.getSnapshotBorders().getTopLeftPosition().getRow();
	$titleStartRow : Integer() from $titleStartRowParam + ($borderStartRow - 1) 
	$titleEndRow   : Integer() from $titleEndRowParam + ($borderStartRow - 1)
	
	// which has a snaphot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.snapshot
	 
	// collect all the other snapshots
	$otherSnapshots: List() from accumulate ( s: ScreenEntityDesigntimeDefinition(this != $screenEntityDefinition), collectList(s.getSnapshot()) )

	// and there is a field from the snapshot in the title rows which matches a title pattern 
	TerminalField($field : this, position.row >= $titleStartRow, position.row <= $titleEndRow, editable == false,value matches $titleRegex) from $snapshot.getFields()
	 
	// and this field doesn't exist on one of the other snapshots (equals by length, text, position and editable) 
	not TerminalSnapshot(fields contains $field) from $otherSnapshots 
then
	screenEntityDefinitionsBuilder.setScreenEntityName(snapshotsAnalyzerContext,$screenEntityDefinition,$field);
end