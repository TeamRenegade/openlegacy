package openlegacy;

rule "identify-actions"
ruleflow-group "identify-actions"
when
	$ruleParameterSet: RuleParametersSet(ruleId == "identify-actions")
	$actionsRegex    : String()  from (String)$ruleParameterSet.getRuleParameters().get("actionsRegex");
	$rowsFromEnd  : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("rowsFromEnd");

	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// which has a snapshot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()

	$actionsStartRow  : Integer()  from $snapshot.getSize().getRows() - $rowsFromEnd + 1  
	$actionsEndRow    : Integer()  from $snapshot.getSize().getRows();

	$field: TerminalField(position.row >= $actionsStartRow,position.row <= $actionsEndRow) from $snapshot.getFields()
	
	$captionAction: String(this matches $actionsRegex) from $field.getValue().split("  ")
	 	
	
then
	int offset = $field.getValue().indexOf($captionAction); 
	TerminalPosition actionPosition = SimpleTerminalPosition.newInstance($field.getPosition().getRow(),$field.getPosition().getColumn()+offset);
	screenEntityDefinitionsBuilder.addAction($screenEntityDefinition,$captionAction,actionPosition, $actionsRegex);
end
