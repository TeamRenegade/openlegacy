package openlegacy;

import org.openlegacy.designtime.terminal.analyzer.support.fact_processors.DateFieldFact;

rule "identify-3-fields-date"
ruleflow-group "identify-field-types"
when
	$ruleParameterSet   : RuleParametersSet(ruleId == "identify-date")
	$dateSeperator		: String()  from (String)$ruleParameterSet.getRuleParameters().get("dateSeperator");

	$dateFieldLength:    Integer() from 2
	$yearFieldMaxLength: Integer() from 4
	$dateFieldsDistance: Integer() from 12
	$numberOfSeperators: Integer() from 2
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// which has a snapshot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()

	// there is an editable field
	// left field - day/month
	$leftDateField: ScreenFieldDefinition(length == $dateFieldLength) from $screenEntityDefinition.getSortedFields() 

	// and there is another field in the size of 2 or 4, which is it's distance from the 1st is not more then 12 columns 
	$rightDateField: ScreenFieldDefinition(
				  this != $leftDateField, 
				  (length == $dateFieldLength || length == $yearFieldMaxLength),
				  position.row == $leftDateField.position.row,
				  position.column > $leftDateField.position.column,
				  position.column <= $leftDateField.position.column + $dateFieldsDistance) from $screenEntityDefinition.getSortedFields()
	  
	// and there is a field between the 1st two
	$middleDateField: ScreenFieldDefinition(
				  this != $leftDateField, this != $rightDateField,  
				  length == $dateFieldLength, 
				  position.row == $leftDateField.position.row,
				  position.column > $leftDateField.position.column,
				  position.column < $rightDateField.position.column) from $screenEntityDefinition.getSortedFields()

	// and there are 2 seperators between these fields
	List(size == $numberOfSeperators) from collect (TerminalField(editable == false,
												 position.row == $leftDateField.position.row,
												 value matches $dateSeperator) from $snapshot.getFields())
then
	screenEntityDefinitionsBuilder.processFact($screenEntityDefinition, new DateFieldFact($leftDateField.getTerminalLabelField(),
																						  $leftDateField.getTerminalField(),
																						  $leftDateField,
																						  $middleDateField,
																						  $rightDateField));
end
