package openlegacy;

import org.openlegacy.designtime.terminal.analyzer.modules.navigation.NavigationFact;

rule "identify-navigation"
ruleflow-group "identify-navigation"
no-loop
when
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition(typeName != "LoginEntity")
	
	// which has a snaphot
	$snapshot: TerminalSnapshot(snapshotType == SnapshotType.INCOMING) from $screenEntityDefinition.getSnapshot()
	
	// find the previous snapshot for a given snapshot
	$previousSequence: Integer() from ($snapshot.getSequence() != null ? $snapshot.getSequence() - 1 : -1)
	$accessedFromSnapshot: TerminalSnapshot(snapshotType == SnapshotType.OUTGOING, sequence > 0,sequence == $previousSequence) from snapshotsAnalyzerContext.getActiveSnapshots()

	// and find the previous snapshot screen definitions
	$accessedFromScreenDefinition: ScreenEntityDesigntimeDefinition(allSnapshots contains $accessedFromSnapshot)


then
	screenEntityDefinitionsBuilder.processFact($screenEntityDefinition, new NavigationFact($accessedFromScreenDefinition,$accessedFromSnapshot));
	update($screenEntityDefinition);
end