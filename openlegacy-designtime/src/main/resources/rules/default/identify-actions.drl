package openlegacy;

import org.openlegacy.designtime.terminal.analyzer.support.fact_processors.ScreenActionFact;

rule "identify-actions"
ruleflow-group "identify-actions"
no-loop
when
	$ruleParameterSet: RuleParametersSet(ruleId == "identify-actions")
	$actionsRegex     : String()  from (String)$ruleParameterSet.getRuleParameters().get("actionsRegex");
	$rowsFromEnd  	  : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("rowsFromEnd");
	$actionsSplitter  : String()  from (String)$ruleParameterSet.getRuleParameters().get("actionsSplitter");

	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// which has a snapshot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()

	$actionsStartRow  : Integer()  from $snapshot.getSize().getRows() - $rowsFromEnd + 1  
	$actionsEndRow    : Integer()  from $snapshot.getSize().getRows();

	$field: TerminalField(position.row >= $actionsStartRow,position.row <= $actionsEndRow) from $snapshot.getFields()
	
	$captionAction: String(this matches $actionsRegex) from $field.getValue().split($actionsSplitter)
	 	
	
then
	int offset = $field.getValue().indexOf($captionAction); 
	TerminalPosition actionPosition = SimpleTerminalPosition.newInstance($field.getPosition().getRow(),$field.getPosition().getColumn()+offset);
	screenEntityDefinitionsBuilder.processFact($screenEntityDefinition,new ScreenActionFact($captionAction,actionPosition, $actionsRegex));
end