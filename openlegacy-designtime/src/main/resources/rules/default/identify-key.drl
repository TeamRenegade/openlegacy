package openlegacy;

import org.openlegacy.terminal.ScreenEntityType;

rule "identify-key"
ruleflow-group "identify-key"
no-loop
salience 100 
no-loop
when
	$ruleParameterSet			: RuleParametersSet(ruleId == "identify-key")
	$minimumFields  				: Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("minimumFields");
	$minimumEditableFields  		: Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("minimumEditableFields");
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition(type == null)

	// and a snapshot from the 1st screen definition
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()

	// with at least X editable fields	
	List(size >= $minimumEditableFields) from collect(ScreenFieldDefinition(editable==true) from $screenEntityDefinition.getSortedFields())

	// and at least Y fields	
	$fieldDefinitions: List(size >= $minimumFields) from collect(ScreenFieldDefinition() from $screenEntityDefinition.getSortedFields())
	
	$fieldDefinition: SimpleScreenFieldDefinition(javaType == Integer.class) from $fieldDefinitions
	 
	not ScreenFieldDefinition(javaType == Integer.class, 
								position.row < $fieldDefinition.position.row || 
								(position.row == $fieldDefinition.position.row && position.column < $fieldDefinition.position.column)
								)  from $fieldDefinitions
								 
then
	$fieldDefinition.setKey(true);
end
