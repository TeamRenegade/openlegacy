package openlegacy;

rule "find-help-text"
ruleflow-group "identify-help-text"
no-loop
when
	$ruleParameterSet    : RuleParametersSet(ruleId == "identify-help-text")
	$helpTextRegex   	 : String()  from (String)$ruleParameterSet.getRuleParameters().get("helpTextRegex");
	$helpTextMaxDistance : Integer() from (Integer)$ruleParameterSet.getRuleParameters().get("helpTextMaxDistance");
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// and a snapshot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()
	
	// and an editable field within the snapshot
	$fieldDefinition: SimpleScreenFieldDefinition(editable == true) from $screenEntityDefinition.getSortedFields()
	
	// and there is a field which is on the right of the field in the format of a help text 
	$helpTextField: TerminalField(editable == false, position.row == $fieldDefinition.position.row, 
									position.column - $fieldDefinition.endPosition.column > 0,
									position.column - $fieldDefinition.endPosition.column < $helpTextMaxDistance,
									value matches $helpTextRegex
									) from $snapshot.getFields()
then
	$fieldDefinition.setHelpText($helpTextField.getValue().trim());
end