package openlegacy;

import org.openlegacy.designtime.terminal.analyzer.support.fact_processors.ScreenWindowFact;
import org.openlegacy.terminal.Color;

rule "identify-window-signs"
ruleflow-group "identify-window"
no-loop 
when
	// capture window in the following pattern
	// "+----+"
	// "|    |"
	// "|    |"
	// "+----+"

	$ruleParameterSet: RuleParametersSet(ruleId == "identify-window")
	$topHorizonalBordersPattern : String()  from (String)$ruleParameterSet.getRuleParameters().get("topHorizonalBordersPattern");
	$buttomHorizonalBordersPattern: String() from (String)$ruleParameterSet.getRuleParameters().get("buttomHorizonalBordersPattern");
	$verticalBorder: String() from (String)$ruleParameterSet.getRuleParameters().get("verticalBorder");
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// which has a snapshot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()
	
	// with a top border field which matches the window horizontal border 
	$topBorderField:    TerminalField(value matches $topHorizonalBordersPattern) from $snapshot.getFields()
	
	// with a buttom border field which matches the window horizontal border, and in the same column & length as the top border, and at lease 2 rows after it   
	$buttomBorderField: TerminalField(position.row >= $topBorderField.position.row+2, position.column == $topBorderField.position.column, 
										length == $topBorderField.length,value matches $buttomHorizonalBordersPattern) from $snapshot.getFields()
	
	// calculate the vertical border position by the start position of the top border field									
	$verticalBorderPosition: Integer() from $topBorderField.position.column
	
	// and there is not row which does NOT have the vertical border char in the vertical border position									
	not TerminalRow(rowNumber > $topBorderField.position.row,rowNumber < $buttomBorderField.position.row,
					// -1 -> text is 0 based, while $verticalBorderPosition is 1 based  
					eval(text.charAt($verticalBorderPosition-1) != $verticalBorder.charAt(0)) )  from $snapshot.getRows() 
	
then
	screenEntityDefinitionsBuilder.processFact($screenEntityDefinition,new ScreenWindowFact($topBorderField,$buttomBorderField));
	// cause "find-screen-name" rule to be re-populated with updated border values
	update($screenEntityDefinition);
end

rule "identify-window-backcolor"
ruleflow-group "identify-window"
no-loop 
when
	// capture window with back color

	$ruleParameterSet: RuleParametersSet(ruleId == "identify-window")
	$backColor : Boolean(booleanValue==true)  from (Boolean)$ruleParameterSet.getRuleParameters().get("backColor");
	$minimumWindowWidth: Integer() from (Integer)$ruleParameterSet.getRuleParameters().get("minimumWindowWidth");
	
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()

	// which has a snapshot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.getSnapshot()
	
	// with a top border field which matches the window horizontal border 
	$topBorderField: TerminalField(length > $minimumWindowWidth, reversed == true) from $snapshot.getFields()
	
	// with a buttom border field which matches the window horizontal border, and in the same column & length as the top border, and at lease 2 rows after it   
	$buttomBorderField: TerminalField(reversed == true,position.row >= $topBorderField.position.row+2, position.column == $topBorderField.position.column, 
										length == $topBorderField.length) from $snapshot.getFields()
	
	// verify there is 1 length reversed fields between the 2 top and buttom borders
	$windowHeight: Integer() from ($buttomBorderField.position.row - $topBorderField.position.row - 1)
	List(size == $windowHeight) from collect (TerminalField(reversed == true, length == 1, position.column == $topBorderField.position.column) from $snapshot.getFields()) 
	List(size == $windowHeight) from collect (TerminalField(reversed == true, length == 1, position.column == $buttomBorderField.position.column) from $snapshot.getFields()) 
then
	screenEntityDefinitionsBuilder.processFact($screenEntityDefinition,new ScreenWindowFact($topBorderField,$buttomBorderField));
	update($screenEntityDefinition);
end
