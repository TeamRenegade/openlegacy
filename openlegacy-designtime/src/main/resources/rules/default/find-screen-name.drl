package openlegacy;

declare FindScreenNameStep
end

rule "pre-find-screen-name"
ruleflow-group "find-screen-name"
salience 110
when
then
	// blocker for next step to avoid multiple evaluation
	insert(new FindScreenNameStep());	
end

rule "find-screen-name"
ruleflow-group "find-screen-name"
no-loop
salience 100
when
	FindScreenNameStep()
	// there is a screen definition
	$screenEntityDefinition: ScreenEntityDesigntimeDefinition()
	
	$ruleParameterSet: RuleParametersSet(ruleId == "find-screen-name")
	$titleRegex    : String()  from (String)$ruleParameterSet.getRuleParameters().get("titleRegex");
	$titleStartRowParam  : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("titleStartRow");
	$titleEndRowParam    : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("titleEndRow");
	$titleStartColumn  : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("titleStartColumn");
	$titleEndColumn    : Integer()  from (Integer)$ruleParameterSet.getRuleParameters().get("titleEndColumn");

	// considering possible window borders
	$borderStartRow: Integer() from $screenEntityDefinition.getSnapshotBorders().getTopLeftPosition().getRow();
	$titleStartRow : Integer() from $titleStartRowParam + ($borderStartRow - 1) 
	$titleEndRow   : Integer() from $titleEndRowParam + ($borderStartRow - 1)
	
	// which has a snaphot
	$snapshot: TerminalSnapshot() from $screenEntityDefinition.snapshot

	// and there is a field from the snapshot in the title rows which matches a title pattern 
	$potentialFieldsForEntityName: List(size > 0) from collect(
											TerminalField(position.row >= $titleStartRow, position.row <= $titleEndRow, 
											// consider title column range only when there is no window boder ($borderStartRow == 1)
											position.column >= $titleStartColumn || $borderStartRow > 1, position.column <= $titleEndColumn || $borderStartRow > 1,
											editable == false,value matches $titleRegex) from $snapshot.getFields()
										)

then
	screenEntityDefinitionsBuilder.selectPotentialScreenEntityName(snapshotsAnalyzerContext,$screenEntityDefinition,$potentialFieldsForEntityName);
	update($screenEntityDefinition);
end